plugins {
    alias(libs.plugins.androidLibrary)
    id 'maven-publish'
}

android {
    namespace 'net.dolearning.libsensor'
    compileSdk 34

    defaultConfig {
        minSdk 24

        testInstrumentationRunner "androidx.test.runner.AndroidJUnitRunner"
        consumerProguardFiles "consumer-rules.pro"

        externalNativeBuild {
            cmake {
                cppFlags ''
            }
        }

        // Similar to other properties in the defaultConfig block,
        // you can configure the ndk block for each product flavor
        // in your build configuration.
        ndk {
            // Specifies the ABI configurations of your native
            // libraries Gradle should build and package with your app.
            abiFilters 'armeabi-v7a', 'arm64-v8a'
        }
    }

    buildTypes {
        release {
            // 配置是否进行代码混淆
            minifyEnabled false
            // 配置代码混淆规则
            proguardFiles getDefaultProguardFile('proguard-android-optimize.txt'), 'proguard-rules.pro'
        }
    }
    compileOptions {
        sourceCompatibility JavaVersion.VERSION_1_8
        targetCompatibility JavaVersion.VERSION_1_8
    }

    ndkVersion "22.1.7171670"

    // Encapsulates your external native build configurations.
    externalNativeBuild {

        // Encapsulates your CMake build configurations.
        cmake {
            path file('CMakeLists.txt')

            version "3.31.1"
        }
    }

    publishing {
        singleVariant('release') {
            withSourcesJar()
        }
    }
}

dependencies {
    // any dependencies marked 'api' will also be available to app module
    api "org.apache.cordova:framework:13.0.0"
    api "com.journeyapps:zxing-android-embedded:3.3.0"

    // dependencies marked 'implementation' will only be available to the current module
    implementation "androidx.webkit:webkit:1.12.1"
    implementation "androidx.activity:activity:1.8.0"

    testImplementation libs.junit
    androidTestImplementation libs.ext.junit
    androidTestImplementation libs.espresso.core
}

publishing {
    publications {
        release(MavenPublication) {
            groupId = 'net.dolearning.learnware'
            artifactId = 'libsensor'
            version = 'v1.0.7'

            afterEvaluate {
                from components.findByName('release')
            }
        }
    }

    // This creates a task called publishReleasePublicationToLibsensorRepository. This task will
    // generate a repository located inside the 'build/lib' folder of the project.
    repositories {
        maven {
            name = 'libsensor'
            url = layout.buildDirectory.dir('lib')
        }
    }
}